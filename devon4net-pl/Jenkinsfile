@Library('ProductionLineTemplateLib')

import com.capgemini.productionline.configuration.*

JenkinsConfiguration jenkinsConfiguration = new JenkinsConfiguration(this);
GitLab gitlabConfiguration;
OpenshiftConfiguration openshiftConfiguration = new OpenshiftConfiguration(this);

pipeline{

    agent any

    //Jenkins Job Parameters
    parameters { 
        string(name: 'PROJECT_NAME', defaultValue: 'devonfw', description: 'Name of the project.') 
        string(name: 'PROJECT_SUFFIX', defaultValue: 'dotnet', description: 'Suffix of the project. For example, if your project name is devonfw and your suffix is dotnet, the project name will be devonfw-dotnet.') 
        string(name: 'GROUP_ID', defaultValue: 'com.devonfw', description: 'The group id of the project.') 
        credentials(credentialType: 'com.dabsquared.gitlabjenkins.connection.GitLabApiTokenImpl', defaultValue: 'gitlab-api-token', description: 'NEEDS TO BE SET!. Private Token of a Production Line Gitlab User that can be used to create repositories.', name: 'GITLAB_USER_PRIVATE_TOKEN', required: true)
        string(name: 'GITLAB_CREATE_GROUP_NAME', defaultValue: 'devonfw', description: 'Name of the group that will be created inside GitLab to clone the repo.') 
        string(name: 'GITLAB_CREATE_PROJECT_DESCRIPTION', defaultValue: 'devonfw for dotnet PL template', description: 'Description of the repository.')
        choice choices: ['none', 'docker', 'openshift'], description: 'Choose the environment where you want to deploy', name: 'DEPLOY'
    }
    
    environment {
        NODE_VERSION = '10.17.0'
    }

    stages {
        stage('PL setup') {
            steps{
                script {
                    if (params.DEPLOY == 'docker' && jenkinsConfiguration.getLastStableBuild('UTILS/Docker_Configuration') == null) {
                        error "In order to deploy using docker, you need to execute the job UTILS/Docker_Configuration as prerequisite"
                    }
                    if (params.DEPLOY == 'openshift' && jenkinsConfiguration.getLastStableBuild('UTILS/Openshift_Configuration') == null) {
                        error "In order to deploy using openshift, you need to execute the job UTILS/Openshift_Configuration as prerequisite"
                    }
                    PROJECT_NAME_FULL = params.PROJECT_NAME + "-" + params.PROJECT_SUFFIX

                    println "Trying to install all required plugins"
                    //Install the required Jenkins plugins
                    //The template currently requires the following plugins to be present
                    def pluginsToInstall = ["ssh-credentials", "custom-tools-plugin", "http_request", "job-dsl", "sonar", "ansible", "pipeline-maven", "nodejs", "gitlab-plugin"]
                    
                    if (params.DEPLOY == 'docker') {
                        pluginsToInstall << "docker-plugin" << "docker-build-step" << "docker-workflow" << "jclouds-jenkins"
                    }

                    if (params.DEPLOY == 'openshift') {
                        pluginsToInstall << 'openshift-client'
                    }

                    def pluginsHaveBeenInstalled = jenkinsConfiguration.installPlugin(pluginsToInstall)
                    
                    if( pluginsHaveBeenInstalled ) {
                        println "New plugins were installed - Restart"
                        // We need to restart Jenkins in case new plugins have been installed.
                        //if set to false, then do a force restart
                        //still requires to manually start the job again afterwards
                        jenkinsConfiguration.restartJenkins(false);
                        error "Restarting Jenkins, please execute pipeline after restart"
                    } else {
                        println "No plugins were installed"
                    }

                    //Add nodejs
                    println "Trying to install nodejs config"
                    String installName = "NodeJS ${NODE_VERSION}"
                    String nodeJS_Version = NODE_VERSION
                    String npmPackages = "yarn"
                    String home = ""
                    long npmPackagesRefreshHours = 100

                    nodeJSInstallSuccess = jenkinsConfiguration.addNodeJS_Version(installName, nodeJS_Version, npmPackages, home, npmPackagesRefreshHours)

                    // Get the Pl Url
                    env.PL_URL = JENKINS_URL.replace("https://", "").split("/")[0]
                    
                    //Add the required script approval
                    println "Adding required script approvals in Jenkins"
                    jenkinsConfiguration.approveSignature("method groovy.lang.GroovyObject invokeMethod java.lang.String java.lang.Object")
                }
            }
        }

        stage('Configuring Openshift'){
            when {
                beforeInput true
                equals expected: 'openshift', actual: params.DEPLOY
            }
            input {
                message 'Introduce the openshift host'
                parameters {
                    string defaultValue: 'default', description: 'Openshift configuration name', name: 'OC_NAME', trim: true
                    credentials(credentialType: 'com.cloudbees.plugins.credentials.impl.UsernamePasswordCredentialsImpl', defaultValue: 'nexus-devon', description: 'Nexus registry integration user credentials', name: 'DOCKER_REGISTRY_CREDENTIALS', required: true)
                }
            }
            environment {
                PORT='8082'
                CPU='100m'
                MEMORY='400Mi'
            }
            tools {
                oc 'OpenShiftv3.11.0'
            }
            steps {
                script {
                    if(!openshiftConfiguration.existsOpenshiftGlobalConfiguration(OC_NAME)) {
                        error "Cluster configuration called ${OC_NAME} does not exists, please create it. You can use the UTILS/Openshift_Configuration template."
                    }
                    env.OC_NAME = OC_NAME

                    sh """wget https://raw.githubusercontent.com/devonfw/devonfw-shop-floor/develop/dsf4openshift/configure-environments/devon4net/devon4net.yaml"""

                    env.SECRET_NAME = "docker-registry-" + env.PL_URL.split("\\.")[0]

                    openshift.withCluster(env.OC_NAME){
                        openshift.withProject() {
                            // Create secret if not exist
                            def secret = openshift.selector('secret', "${env.SECRET_NAME}")
                            if (!secret.exists()) {
                                echo "secret not exist"
                                withCredentials([usernamePassword(credentialsId: "${DOCKER_REGISTRY_CREDENTIALS}", passwordVariable: 'drpass', usernameVariable: 'druser')]) {
                                    openshift.create('secret', "docker-registry ${env.SECRET_NAME} --docker-server=docker-registry-${env.PL_URL} --docker-username=${druser} --docker-password=${drpass} --docker-email=no-reply@email.com")
                                }
                            }

                            // Create environments for dev, uat and prod
                            openshift.create( openshift.process( '', '-f', 'devon4net.yaml', "-p", "APPLICATION_NAME=${PROJECT_NAME_FULL}", "-p", "APPLICATION_GROUP_NAME=${params.PROJECT_NAME}", "-p", "APPLICATION_NAME_SUFFIX=-dev", "-p", "DOCKER_IMAGE=docker-registry-${env.PL_URL}/${PROJECT_NAME_FULL}", "-p", "DOCKER_TAG=latest", "-p", "SECRET=${env.SECRET_NAME}", "-p", "CPU=${CPU}", "-p", "MEMORY=${MEMORY}", "-p", "PORT=${PORT}"  ) )
                            openshift.create( openshift.process( '', '-f', 'devon4net.yaml', "-p", "APPLICATION_NAME=${PROJECT_NAME_FULL}", "-p", "APPLICATION_GROUP_NAME=${params.PROJECT_NAME}", "-p", "APPLICATION_NAME_SUFFIX=-uat", "-p", "DOCKER_IMAGE=docker-registry-${env.PL_URL}/${PROJECT_NAME_FULL}", "-p", "DOCKER_TAG=release", "-p", "SECRET=${env.SECRET_NAME}", "-p", "CPU=${CPU}", "-p", "MEMORY=${MEMORY}", "-p", "PORT=${PORT}"  ) )
                            openshift.create( openshift.process( '', '-f', 'devon4net.yaml', "-p", "APPLICATION_NAME=${PROJECT_NAME_FULL}", "-p", "APPLICATION_GROUP_NAME=${params.PROJECT_NAME}", "-p", "APPLICATION_NAME_SUFFIX=-stage", "-p", "DOCKER_IMAGE=docker-registry-${env.PL_URL}/${PROJECT_NAME_FULL}", "-p", "DOCKER_TAG=production", "-p", "SECRET=${env.SECRET_NAME}", "-p", "CPU=${CPU}", "-p", "MEMORY=${MEMORY}", "-p", "PORT=${PORT}"  ) )
                        }
                    }
                }
            }
        }
        
        stage('Create devon4net repo') {
            steps{
                script{
                    println "Trying to create gitlab group"
                    // Get GitLab Token and define GitLab configuration.
                    def token = GitLab.gitlabApiToken(GITLAB_USER_PRIVATE_TOKEN);
                    gitlabConfiguration = new GitLab(this, token, ProductionLineGlobals.GITLAB_BASE_URL);

                    //Check if group already exists
                    if(gitlabConfiguration.getGroupId(params.GITLAB_CREATE_GROUP_NAME) == ""){
                        println "No group existing yet, trying to create"
                        //Create a group for devonfw
                        // public createGroup(String groupname, String grouppath, String groupdesc, String grouptype) {
                        gitlabConfiguration.createGroup(params.GITLAB_CREATE_GROUP_NAME, params.GITLAB_CREATE_GROUP_NAME, params.GITLAB_CREATE_PROJECT_DESCRIPTION, "public")
                    } else {
                        println "Group already exists"
                    }
                    //Check if project already exists
                    println "Trying co create Gitlab project"
                    if(gitlabConfiguration.getProjectId(params.GITLAB_CREATE_GROUP_NAME, PROJECT_NAME_FULL) == ""){
                        println "No project with the same name exists yet, trying to create"
                        //Create a new public repository for devon4ng in the Production Line
                        gitlabConfiguration.createProject(
                            params.GITLAB_CREATE_GROUP_NAME, 
                            PROJECT_NAME_FULL, 
                            PROJECT_NAME_FULL, 
                            params.GITLAB_CREATE_PROJECT_DESCRIPTION, 
                            "develop", 
                            "public"
                        )
                    } else {
                        println "Project with the same name already exists"
                    }
                    gitlabConfiguration.createWebhook(params.GITLAB_CREATE_GROUP_NAME, PROJECT_NAME_FULL, "http://jenkins-core:8080/jenkins/project/${PROJECT_NAME}/${PROJECT_NAME_FULL}", '')
                }
            }
        }

        stage('Generate dotnet template job'){
            steps{
                println "Trying to create Jenkins jobs"
                script{
                    //Disable Job Security
                    println "Disable job dsl security"
                    jenkinsConfiguration.disableJobDSLScriptSecurity()

                    //Prepare folders in Jenkins for the jobs
                    println "Trying to create folder for jobs"
                    jobDsl scriptText: """
                        folder("${PROJECT_NAME}"){
                            description('Jobs for ${PROJECT_NAME} project.')
                        }
                        """

                    //Build job
                    println "Trying to create Build job"
                    jobDsl scriptText: """
                        multibranchPipelineJob("${PROJECT_NAME}/${PROJECT_NAME_FULL}") {
                            description('Build job for ${PROJECT_NAME_FULL}.')
                            branchSources {
                                git {
                                    remote('${ProductionLineGlobals.GITLAB_BASE_URL}/${params.GITLAB_CREATE_GROUP_NAME}/${PROJECT_NAME_FULL}.git')
                                    credentialsId('github-ci')
                                    includes('master release* develop')
                                }
                            }
                            orphanedItemStrategy {
                                discardOldItems {
                                    numToKeep(10)
                                }
                            }
                        }
                        """

                    //Enable Job Security again
                    println "Enable job dsl security"
                    jenkinsConfiguration.enableJobDSLScriptSecurity()
                }
            }
        }
        
        stage('Install dotnet custom tool') {
            steps{
                script {
                    jenkinsConfiguration.addCustomTool('dotnet', '', """if ! which dotnet > /dev/null; then
wget -qO- https://packages.microsoft.com/keys/microsoft.asc | gpg --dearmor > microsoft.asc.gpg
sudo mv microsoft.asc.gpg /etc/apt/trusted.gpg.d/
wget -q https://packages.microsoft.com/config/debian/10/prod.list
sudo mv prod.list /etc/apt/sources.list.d/microsoft-prod.list
sudo chown root:root /etc/apt/trusted.gpg.d/microsoft.asc.gpg
sudo chown root:root /etc/apt/sources.list.d/microsoft-prod.list
sudo apt-get update
sudo apt-get install -y apt-transport-https
sudo apt-get update
sudo apt-get install -y dotnet-sdk-3.0
fi""", '/usr/bin')
                }
            }
        }

        stage('Install cicdgen') {
            tools {
                nodejs "NodeJS ${NODE_VERSION}"
            }
            steps{
                cleanWs()
                deleteDir()

                sh 'npm i -g @devonfw/cicdgen'
            }
        }

        stage('Generate dotnet project') {
            steps{
                script {
                    tool 'dotnet'
                    sh "mkdir ${PROJECT_NAME_FULL}"
                    dir(PROJECT_NAME_FULL){
                        sh "dotnet new --install Devon4Net.WebAPI.Template"
                        sh "dotnet new Devon4NetAPI"
                    }
                }
            }
        }
        
        stage('Create cicdgen dotnet template') {
            tools {
                nodejs "NodeJS ${NODE_VERSION}"
            }
            steps{
                script{ 
                    def dockerString = ''
                    def ocString = ''

                    if (params.DEPLOY == 'docker'){
                        dockerString = "--docker --plurl ${env.PL_URL} "
                    }

                    if (params.DEPLOY == 'openshift'){
                        ocString = "--openshift --ocname ${env.OC_NAME} --plurl ${env.PL_URL} "
                    }
                
                    dir(PROJECT_NAME_FULL) {
                        sh "git init"
                        sh 'git config user.email "jenkins-pl@capgemini.com"'
                        sh 'git config user.name "Jenkins"'
                        sh "cicdgen generate devon4net ${ocString}${dockerString}--groupid ${params.GROUP_ID} --appname ${PROJECT_NAME_FULL} --appversion 0.0.1"
                    }   
                }
            }
        }
        
        stage('Pushing template to Gitlab') {
            steps{
                script {
                    dir(PROJECT_NAME_FULL){
                        def token = GitLab.gitlabApiToken(GITLAB_USER_PRIVATE_TOKEN);
                        sh "git status && git remote add origin http://oauth2:${token}@gitlab-core:80/gitlab/${GITLAB_CREATE_GROUP_NAME}/${PROJECT_NAME_FULL}.git"
                        sh "git checkout -b develop && git push --set-upstream origin develop"
                        
                        // sh """git add . && git commit -m "start 0.0.1 version" && git push --set-upstream origin develop"""
                        sh "git checkout master && git push origin master"
                        gitlabConfiguration.protectBranches(params.GITLAB_CREATE_GROUP_NAME, PROJECT_NAME_FULL, "master", "40", "30");
                        gitlabConfiguration.unprotectBranch(params.GITLAB_CREATE_GROUP_NAME, PROJECT_NAME_FULL, "develop");
                        gitlabConfiguration.protectBranches(params.GITLAB_CREATE_GROUP_NAME, PROJECT_NAME_FULL, "develop", "30", "30");
                        gitlabConfiguration.protectBranches(params.GITLAB_CREATE_GROUP_NAME, PROJECT_NAME_FULL, "release/*", "40", "30");
                        gitlabConfiguration.protectBranches(params.GITLAB_CREATE_GROUP_NAME, PROJECT_NAME_FULL, "release", "40", "30");
                    }    
                }
            }
        }
    }

    post{
        always{
            cleanWs()
            deleteDir()
        }
    }
}
